A single object is defined as follows...

[groovy]
----
gina(Book, title: "Groovy In Action")
----
This is defining an object named @gina@, of type @Book@ with the @title@ property set to @"Groovy In Action"@.

An alternative syntax is...

[groovy]
----
gina(Book) {
    title =  "Groovy In Action"
}
----

These two syntaxes effectively achieve the same thing. The difference is that the first example is equivalent to...

[groovy]
----
def gina = new Book(title: "Groovy In Action")
----

While the second (alternative) syntax is equivalent to...

[groovy]
----
def gina = new Book()
gina.title = "Groovy In Action"
----

The two can even be mixed...

[groovy]
----
gina(Book, title: "Groovy In Action") {
    isbn = "123456778"
}
----


= Specifying Relationships

A key benefit to using the fixtures plugin is that it makes specifying related objects simple and concise.

Consider the following GORM domain classes:

[groovy]
----
class Author {
    static hasMany = [books: Book]
    String name
}
class Book {
    String title
}
----

We can specify an Author/Book relationship like so...

[groovy]
----
b(Book, title: "The Fellowship of the Ring")
a(Author, name: "J.R.R. Tolkien", books: [b])
----

In this example, @b@ must be defined before it is referenced by @a@. To reference @b@ before it is defined, the special @ref(«name»)@ method can be used...

[groovy]
----
a(Author, name: "J.R.R. Tolkien", books: [ref("b")])
b(Book, title: "The Fellowship of the Ring")
----

If the relationship is bidirectional, it can be set on either side...

[groovy]
----
a(Author, name: "J.R.R. Tolkien")
b(Book, title: "The Fellowship of the Ring", author: a)
----

For one-to-many associations, you can also use a nested syntax with version 1.2 of the plugin and above that reduces the verbosity of the fixture data and improves readabililty. So the previous examples can now be written like:

[groovy]
----
a(Author, name: "J.R.R. Tolkien", books: [
   new Book(title: "The Fellowship of the Ring") ])
----

You can nest to arbitrary depths too. Imagine that a book can have many @Label@ instances:

[groovy]
----
a(Author, name: "J.R.R. Tolkien", books: [
   new Book(title: "The Fellowship of the Ring", labels: [
      new Label(name: "fantasy"),
      new Label(name: "classic") ]),
   new Book(title: "The Two Towers")])
----

Simply nest new instances of the appropriate type for the collection, e.g. @Book@ for the @books@ property.
